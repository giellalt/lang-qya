# converting from underlying form to normal Latin orthography

define Vowel [a|e|i|o|u|á|é|í|ó|ú];
define InputAlphabet [q|w|%{w%}|e|é|r|t|y|u|ú|i|í|o|ó|ö|p|a|á|s|þ|d|f|g|h|j|k|l|z|x|c|v|b|n|ñ|m];
define WordBoundary [\InputAlphabet | .#.];

! start by getting rid of flag diacritics
! define DeleteFlagsRule [
!     "@U.AdjSup.TRUE@" -> 0 , "@U.AdjSup.FALSE@" -> 0 ,
!     "@U.Case.Voc@" -> 0 , "@R.Case.Voc@" -> 0 , "@D.Case.Voc@" -> 0 ,
!     "@U.Tense.Perf@" -> 0 , "@R.Tense.Perf@" -> 0 , "@D.Tense.Perf@" -> 0 ,
!     "@U.Perf.NoPrefix@" -> 0 , "@U.Perf.YesPrefix@" -> 0
! ];

define WilyaRule1 [ %{w%} -> v || WordBoundary _ ];
define WilyaRule2 [ %{w%} -> v || Vowel _ Vowel ];
define WilyaRule3 [ %{w%} -> w ];

define SuleRule [ þ -> s ];

# diaeresis
define FinalENotAloneRule [ e -> ë || Vowel ?* _ .#. ];
define NotDiphthongERule1 [ e -> ë || _ [a|o] ];
define NotDiphthongERule2 [ e -> ë || o _ ];
define CapitalEARule [ a -> ä || E _ ];
define CapitalEORule [ o -> ö || E _ ];

read regex [
!     DeleteFlagsRule .o.
    WilyaRule1  .o.
    WilyaRule2  .o.
    WilyaRule3  .o.
    SuleRule    .o.
    FinalENotAloneRule  .o.
    NotDiphthongERule1  .o.
    NotDiphthongERule2  .o.
    CapitalEARule   .o.
    CapitalEORule
];
