!! Adjectives
!  -----
!! Adjectives in the Quenya language give descriptions or attributes.

LEXICON Adjectives
    AdjPrefix ;

LEXICON AdjPrefix
    @U.AdjSup.TRUE@:@U.AdjSup.TRUE@a{n}%< AdjStems ;  ! TODO: add twolc rules for {n} and <
    @U.AdjSup.FALSE@ AdjStems ;

LEXICON AdjStems
    ! Lesson 4
    vanya:vany              AdjSuffix       "beautiful, fair" ;
    alta:alt                AdjSuffix       "large, great" ;
    calima:calim            AdjSuffix       "bright" ;
    taura:taur              AdjSuffix       "mighty, masterful" ;
    saila:sail              AdjSuffix       "wise" ;
    úmëa:úm                 AdjSuffix-EA    "abundant, swarming, teeming" ;
    carnë:carn{stemi2e}     AdjSuffix-Spec  "red" ;
    ninquë:ninqu            AdjSuffix-E     "white" ;
    morë:mor                AdjSuffix-E     "black" ;
    firin                   AdjSuffix-Cons  "dead" ;
    ! Assorted
    laurëa:laur             AdjSuffix-EA    "golden, like gold" ;
    hiswa:hiþw              AdjSuffix       "grey" ;
    hloirëa:hloir           AdjSuffix-EA    "venomous" ;
    moina:moin              AdjSuffix       "familiar, dear" ;
    sarta:sart              AdjSuffix       "steadfast, trusty, loyal" ;
    tyelca:tyelc            AdjSuffix       "swift, agile" ;
    nwalca:ñwalc            AdjSuffix       "cruel" ;
    rempa:remp              AdjSuffix       "crooked, hooked" ;
    sanda:þand              AdjSuffix       "firm, true, abiding" ;
    pahta:paht              AdjSuffix       "closed, shut, private" ;

! vim: set ft=xfst-lexc:
