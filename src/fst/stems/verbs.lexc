! Quenya verbs are conjugated for tense/aspect (past, present perfect, present progressive, present simple (aorist), future)
! and receive suffixes as pronouns for subject and object

! ^ is used to mark the root vowel that is lengthened in some forms

LEXICON Verbs
    ! no perfective prefix
    @U.Perf.NoPrefix@                   VerbStems ;
    @U.Perf.NoPrefix@                   VerbStems-Vowel ;
    ! yes perfective prefix
    @U.Perf.YesPrefix@:@U.Perf.YesPrefix@{R}    VerbStems ;
    @U.Perf.YesPrefix@:@U.Perf.YesPrefix@{R}{R} VerbStems-Vowel ;
    ! special verbs
    Copula ;
    Existential ;
    Negative ;
    equë:eque           VerbSubject         "say, said" ;
    ! #hriz-

LEXICON VerbStems  ! verb stems that start with a consonant
    car-:c{^a}r         VerbSuffix-Cons     "do, make" ;
    calpa-:calp         VerbSuffix-A        "draw water, scoop out, bale out" ;
    cav-:c{^a}{w}       VerbSuffix-Cons     "bow" ;
    cen-:c{^e}n         VerbSuffix-Cons     "see, behold" ;
    ces-:c{^e}þ         VerbSuffix-Cons     "enquire of, question, examine" ;
    ceuta-:ceut         VerbSuffix-A        "renew, refresh" ;
    col-:c{^o}l         VerbSuffix-Cons     "bear, carry" ;
    hat-:h{^a}t         VerbSuffix-Cons     "fling" ;
    hócir-:hóc{^i}r     VerbSuffix-Cons     "cut off (to then take or use it)" ;
    ! lav as lick has a special past tense
    lav-:l{^a}v         VerbSuffix-Cons     "yield, allow, grant" ;
    tana-:t{^a}n        VerbSuffix-A        "show, indicate" ;
    tec-:t{^e}c         VerbSuffix-Cons     "write" ;
    top-:t{^o}p         VerbSuffix-Cons     "cover" ;

LEXICON VerbStems-Vowel  ! verbs stems that start with a vowel
    alya-:al            VerbSuffix-YA       "cause to prosper, bless, help" ;
    anta-:ant           VerbSuffix-A        "give" ;
    ista-:ist           VerbSuffix-A        "know" ;  ! irregular past tense: sintë
    órava-:órav         VerbSuffix-A        "have mercy" ;
    us-:{^u}s           VerbSuffix-Cons     "escape" ;


! special verbs
! TODO

LEXICON Copula  ! state or attribute
    ná+V+Pres:ná        VerbSubject ;
    ná+V+Pres+Plur:nar  # ;
    ná+V+Past:né        VerbSubject ;
    ná+V+Fut:nauva      VerbSubject ;
    ná+V+Imp:na         # ;

LEXICON Existential  ! presence or existence, perhaps position
    ëa+V+Past:enge      VerbSubject ;
    ëa+V+Pres:ea        VerbSubject ;
    ëa+V+Fut:euva       VerbSubject ;
    ëa+V+Imp:ea         # ;

LEXICON Negative
    úm-+V+Neg:úm        VerbSuffix-Cons ;
