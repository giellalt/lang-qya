!! Noun inflection
!  ---------------
!! The Quenya language nouns inflect in cases.


! Standard declensions (nouns that end with -a, -i, -ie, -o, -u)

LEXICON NounSuffix
    +N:%> Cases ;

LEXICON Cases
    +Sing:      CasesSing ;
    +Dual:      CasesDual ;
    +Plur:      CasesPlur ;
    +Plur+Par:  CasesPlurPar ;

LEXICON CasesSing
    +Nom: # ;
    +Acc: # ;
    +Gen:{o}  # ;
    +Ins:nen # ;
    +All:nna # ;
    +Dat:n # ;
    +Loc:sse # ;
    +Loc+Short:s # ;
    +Abl:llo # ;
    +Poss:{w}a # ;  ! TODO: connect to adjectival endings?

LEXICON CasesDual
    +Nom:t # ;
    +Acc:t # ;
    +Gen:to  # ;
    +Ins:nten # ;
    +All:nta # ;
    +Dat:nt # ;
    +Loc:tse # ;
    ! +Loc+Short:ts # ;  TODO: ???
    +Abl:lto # ;
    +Poss:t{w}a # ;  ! TODO: connect to adjectival endings?
    CasesDual-U ;  ! also go to the dual endings in -u

LEXICON CasesPlur
    +Nom:r # ;
    +Acc:r # ;
    +Gen:ron  # ;
    +Ins:{i}nen # ;
    +All:nnar # ;
    +Dat:{i}n # ;
    +Loc:ssen # ;
    +Loc+Short:{i}s # ;
    +Abl:llon # ;
    +Poss:{i}{w}a # ;  ! TODO: connect to adjectival endings?

LEXICON CasesPlurPar
    +Nom:li # ;
    +Acc:li # ;
    +Gen:lion  # ;
    +Ins:línen # ;
    +All:linna # ;  ! r is optional for allative
    +All:linnar # ;
    +Dat:lin # ;
    +Loc:lisse # ;  ! n is optional for locative
    +Loc:lissen # ;
    +Loc+Short:lis # ;
    +Abl:lillo # ;  ! n is optional for ablative
    +Abl:lillon # ;
    +Poss:lí{w}a # ;  ! TODO: connect to adjectival endings?


! -e declensions (nouns that end with -e)

LEXICON NounSuffix-E
    +N:%> Cases-E ;

LEXICON Cases-E
    +Sing:      CasesSing-E ;
    +Dual:      CasesDual-E ;
    +Plur:      CasesPlur-E ;
    +Plur+Par:  CasesPlurPar-E ;

LEXICON CasesSing-E
    +Nom: # ;
    +Acc: # ;
    +Gen:o  # ;
    +Ins:nen # ;
    +All:nna # ;
    +Dat:n # ;
    +Loc:sse # ;
    +Loc+Short:s # ;
    +Abl:llo # ;
    +Poss:{w}a # ;  ! TODO: connect to adjectival endings?

LEXICON CasesDual-E
    +Nom:t # ;
    +Acc:t # ;
    +Gen:to  # ;
    +Ins:nten # ;
    +All:nta # ;
    +Dat:nt # ;
    +Loc:tse # ;
    ! +Loc+Short:ts # ;  TODO: ???
    +Abl:lto # ;
    +Poss:t{w}a # ;  ! TODO: connect to adjectival endings?
    CasesDual-U ;  ! also go to the dual endings in -u

LEXICON CasesPlur-E
    +Nom:{i} # ;
    +Acc:{i} # ;
    +Gen:{i}on  # ;
    +Ins:{í}nen # ;
    +All:nnar # ;
    +Dat:{í}n # ;
    +Loc:ssen # ;
    +Loc+Short:{í}s # ;
    +Abl:llon # ;
    +Poss:{í}{w}a # ;  ! TODO: connect to adjectival endings?

LEXICON CasesPlurPar-E
    +Nom:li # ;
    +Acc:li # ;
    +Gen:lion  # ;
    +Ins:línen # ;
    +All:linna # ;  ! r is optional for allative
    +All:linnar # ;
    +Dat:lin # ;
    +Loc:lisse # ;  ! n is optional for locative
    +Loc:lissen # ;
    +Loc+Short:lis # ;
    +Abl:lillo # ;  ! n is optional for ablative
    +Abl:lillon # ;
    +Poss:lí{w}a # ;  ! TODO: connect to adjectival endings?


! -Cons declensions (nouns that end with a consonant)

LEXICON NounSuffix-Cons
    +N:%> Cases-Cons ;

LEXICON Cases-Cons
    +Sing:      CasesSing-Cons ;
    +Dual:      CasesDual-Cons ;
    +Plur:      CasesPlur-Cons ;
    +Plur+Par:  CasesPlurPar-Cons ;

LEXICON CasesSing-Cons
    +Nom: # ;
    +Acc: # ;
    +Gen:o  # ;
    +Ins:nen # ;
    +All:nna # ;
    +Dat:n # ;
    +Loc:sse # ;
    +Loc+Short:s # ;
    +Abl:llo # ;
    +Poss:{w}a # ;  ! TODO: connect to adjectival endings?

LEXICON CasesDual-Cons
    +Nom:t # ;
    +Acc:t # ;
    +Gen:to  # ;
    +Ins:nten # ;
    +All:nta # ;
    +Dat:nt # ;
    +Loc:tse # ;
    ! +Loc+Short:ts # ;  TODO: ???
    +Abl:lto # ;
    +Poss:t{w}a # ;  ! TODO: connect to adjectival endings?
    CasesDual-U ;  ! also go to the dual endings in -u

LEXICON CasesPlur-Cons
    +Nom:i # ;
    +Acc:i # ;
    +Gen:ion  # ;
    +Ins:inen # ;
    +All:innar # ;
    +Dat:in # ;
    +Loc:issen # ;
    +Loc+Short:is # ;
    +Abl:illon # ;
    +Poss:i{w}a # ;  ! TODO: connect to adjectival endings?

LEXICON CasesPlurPar-Cons
    +Nom:{l}i # ;
    +Acc:{l}i # ;
    +Gen:{l}ion  # ;
    +Ins:{l}ínen # ;
    +All:{l}inna # ;  ! r is optional for allative
    +All:{l}innar # ;
    +Dat:{l}in # ;
    +Loc:{l}isse # ;  ! n is optional for locative
    +Loc:{l}issen # ;
    +Loc+Short:{l}is # ;
    +Abl:{l}illo # ;  ! n is optional for ablative
    +Abl:{l}illon # ;
    +Poss:{l}í{w}a # ;  ! TODO: connect to adjectival endings?


! dual endings that use -u (not -t)

LEXICON CasesDual-U
    +Nom:{-V}u # ;
    +Acc:{-V}u # ;
    +Gen:{-V}uo  # ;
    +Ins:{-V}unen # ;
    +All:{-V}unna # ;
    +Dat:{-V}un # ;
    +Dat:{-V}uen # "weight: -1.0" ;  ! poorly attested
    +Loc:{-V}usse # ;
    +Loc+Short:{-V}us # ;
    +Abl:{-V}ullo # ;
    +Poss:{-V}u{w}a # ;  ! TODO: connect to adjectival endings?

! vim: set ft=xfst-lexc:
